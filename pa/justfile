# The default recipe is always the first recipe in the justfile

# The recipe to run when just is invoked without a recipe
default: list

# List available recipes
list:
    @just --list

build_dir := 'target'

setup build_type=default_build_type flags='--reconfigure':
    meson setup {{build_dir}} {{flags}} --backend=ninja --buildtype={{build_type}}
    ln -sf {{build_dir}}/compile_commands.json compile_commands.json
default_build_type := 'release'

wipe build_type=default_build_type: (setup build_type '--wipe')

build:
    meson compile -C {{build_dir}}

run args='': build
    ./{{build_dir}}/main {{args}}

clean:
    meson compile -C {{build_dir}} --clean

distclean:
    rm -rf {{build_dir}}
    rm -f compile_commands.json

# Format all C files
format:
    clang-format -i $(fd '\.c' --full-path "$(git rev-parse --show-toplevel)")
alias fmt := format
